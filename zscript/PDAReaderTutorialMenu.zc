/****************************************************************************

MIT License

Copyright (c) 2019 - 2021 Nash Muhandes

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.

****************************************************************************/

//===========================================================================
//
// PDAReaderTutorialMenu
//
//===========================================================================


class PDAReaderTutorialMenu : GenericMenu
{
	BrokenLines mMessage;
	Font textFont;
	int destWidth, destHeight;

	override void Init(Menu parent)
	{
		Super.Init(parent);

		String sInvUse;
		Array<int> invUseKeys;

		Bindings.GetAllKeysForCommand(invUseKeys, "invuse");
		sInvUse = KeyBindings.NameAllKeys(invUseKeys);

		String message = StringTable.Localize("$PDA_READER_TUTORIAL");

		textFont = OptionFont();
		mMessage = textFont.BreakLines(String.Format(message, sInvUse), int(300 / NotifyFontScale));
		destWidth = CleanWidth;
		destHeight = CleanHeight;
	}

	override void Drawer(void)
	{
		Super.Drawer();

		int i;
		double y;
		let fontheight = textFont.GetHeight() * NotifyFontScale;

		y = destHeight / 2;

		int c = mMessage.Count();
		y -= c * fontHeight / 2;

		for (i = 0; i < c; i++)
		{
			Screen.DrawText (textFont, Font.CR_UNTRANSLATED, destWidth / 2 - mMessage.StringWidth(i) * NotifyFontScale / 2, y, mMessage.StringAt(i),
				DTA_VirtualWidth, destWidth, DTA_VirtualHeight, destHeight,
				DTA_KeepRatio, true,
				DTA_ScaleX, NotifyFontScale,
				DTA_ScaleY, NotifyFontScale);
			y += fontheight;
		}
	}

	override bool MenuEvent(int mkey, bool fromcontroller)
	{
		switch (mkey)
		{
			case MKEY_Enter:
			case MKEY_Back:
			default:
				Close();
				break;
		}
		return Super.MenuEvent(mkey, fromcontroller);
	}

	override bool MouseEvent(int type, int x, int y)
	{
		if (type == MOUSE_Release)
		{
			if (MenuEvent(MKEY_Enter, true))
			{
				return true;
			}
		}
		return Super.MouseEvent(type, x, y);
	}
}
